Let ( [
  #date = GetAsDate ( startDate );
  #day = DayOfWeek ( #date );
  #pastFuture = If (offset < 0; "past"; "future" );
  #theStart = 
    Case ( 
      #day = 7 and #pastFuture = "future"; // Saturday
      #date - 1;

      #day = 1 and #pastFuture = "future"; // Sunday
      #date - 2;

      #day = 1 and #pastFuture = "past"; // Sunday
      #date + 1;

      #day = 7 and #pastFuture = "past"; // Saturday
      #date + 2;

      #date
    )
  ];

  Case (
    #pastFuture = "past";
      #theStart + Int (offset / 5 ) * 7 - 
      Middle ("23456034560145601256012360123412345" ; 
        (DayOfWeek (#theStart ) - 1 ) * 5 + Mod (Abs (offset ); 5 ) + 1;
        1
      );
     
    #theStart + Int (offset / 5 ) * 7 + 
    Middle ("12345012340123601256014560345623456" ; 
      (DayOfWeek (#theStart ) - 1 ) * 5 + Mod (offset; 5 ) + 1; 
      1 
    )
  )
)

/* —————————————————————————————— //
NAME:
	WeekdaysOffset ( startDate; offset )

PURPOSE:
	Returns the specified date plus or minus the offset. 
	Only includes weekdays (Monday - Friday), including holidays.

EXAMPLES:
	WeekdaysOffset ( "10/1/10" ; 3 ) = 10/6/2010

HISTORY:
	Created: Nick Chapin
	Modified: 2010-Oct-12 10h00 PST — Donovan Chandler (Reformatted and refactored slightly)
*/