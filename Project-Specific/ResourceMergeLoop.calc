Let ( [
  index = repetition ; 
  marker = "‡‡RESOURCE[" & index & "]‡‡" ; 
  markerPath = "‡‡RESOURCE_PATH[" & index & "]‡‡" ; 
  textNew =
    //-- Find and replace marker for this index ( assuming there's only one)
    Case ( 
      PatternCount ( text ; marker ) ; 
        Substitute ( text ; marker ; ResourceInclude ( index ; 0 )) ; 
      PatternCount ( text ; markerPath ) ; 
        Substitute ( text ; markerPath ; ResourceInclude ( index ; 1 )) ; 
      text
    )
  ] ; 
  //-- Continue thru other Resources
  Case ( 
    PatternCount ( textNew ; "‡‡RESOURCE" ) ; 
      ResourceMergeLoop ( textNew ; repetition + 1 ) ; 
    textNew
  )
)

/* —————————————————————————————— //
NAME:
  ResourceMergeLoop ( text ; repetition )

PURPOSE:
  Performs operations for ResourceMerge ( ).

EXAMPLES:
  

HISTORY:
  Created: 2010-Sep-01 11h27 PST — Donovan Chandler
*/