Let ( [
  start = GetAsDate ( startDate ) ;
  dayOfWeek = DayOfWeek ( start ) ;
  dateCur =
    Case (
      omitWeekends ;
        Case (
          dayOfWeek = 7 ; start + 2 ; // Skip Sat & Sun
          dayOfWeek = 1 ; start + 1 ; // Skip Sun
          start
        ) ;
      start
    ) ;
  result = List ( previousResults ; dateCur ) ;
  resultCount = ValueCount ( result )
  ] ;
  Case (
    resultCount ≥ numberOfDaysToReturn ;
      result ;

    DatesFromStartLoop (
      dateCur + 1 ;
      numberOfDaysToReturn ;
      omitWeekends ;
      omitHolidays ;
      result
    )

  )
)

/* —————————————————————————————— //
NAME:
  DatesFromStartLoop ( startDate ; numberOfDaysToReturn ; omitWeekends ; omitHolidays ; previousResults )

PURPOSE:
  Performs recursive operation for DatesFromStart().  Returns list of dates as specified. 

EXAMPLES:
  DatesFromStartLoop ( "10/2/2010" ; 2 ; 1 ; 0 ; "10/1/2010" ) = "10/1/2010¶10/4/2010"

HISTORY:
  Created: 2010-Oct-15 12h11 PST — Donovan A. Chandler

NOTE:
    omitHolidays parameter currently not supported.
*/