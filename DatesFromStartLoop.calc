Let ( [
  start = GetAsDate ( startDate ) ;
  dayOfWeek = DayOfWeek ( start ) ;
  holidayList = ListDateToNumber ( DatesHolidayList ) ;
  isWeekend =
    Case (
      omitWeekends ;
        dayOfWeek = 7 or dayOfWeek = 1 ;
      0
    ) ;
  isHoliday =
    Case (
      omitHolidays ;
        ValueCount ( FilterValues ( holidayList ; GetAsNumber ( start ) ) ) > 0 ;
      0
    ) ;
  result = 
    Case (
      isWeekend or isHoliday ;
        previousResults ;
      List ( previousResults ; start )
    ) ;
  resultCount = ValueCount ( result )
  ] ;
  Case (
    resultCount ≥ numberOfDaysToReturn ;
      result ;

    DatesFromStartLoop (
      start + 1 ;
      numberOfDaysToReturn ;
      omitWeekends ;
      omitHolidays ;
      result
    )

  )
)

/* —————————————————————————————— //
NAME:
  DatesFromStartLoop ( startDate ; numberOfDaysToReturn ; omitWeekends ; omitHolidays ; previousResults )

PURPOSE:
  Performs recursive operation for DatesFromStart().  Returns list of dates as specified. 

EXAMPLES:
  DatesFromStartLoop ( "10/2/2010" ; 2 ; 1 ; 0 ; "10/1/2010" ) = "10/1/2010¶10/4/2010"

HISTORY:
  Created: 2010-Oct-15 12h11 PST — Donovan A. Chandler

NOTE:
    omitHolidays parameter currently not supported.
*/