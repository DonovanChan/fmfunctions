Let ( [
	directory = directory ;
	typeFull =
		Case (
			Abs ( Get ( SystemPlatform ) ) = 1 ; "filemac:/" ;
			Abs ( Get ( SystemPlatform ) ) = 2 ; "filewin:/" ;
		) ;
	typePath =
		Case (
			pathType = "relative"	; "file:" ;
			pathType = "full"	; typeFull ;
			pathType = "remote"	; "filewin://" & Get ( HostName ) ;
			pathType = "network"	; "fmnet:" & Get ( HostIPAddress ) & "/" ;
			pathType = "web"	; "file:/"
		) ;
	dirPathA =
		Case (
			directory = "desktop"		; Get ( DesktopPath ) ;
			directory = "documents"	; Get ( DocumentsPath ) ;
			directory = "temporary"	; Get ( TemporaryPath ) ;
			directory = "preferences"	; Get ( PreferencesPath ) ;
			directory = "drive"		; Get ( SystemDrive ) ;
			directory = "filemaker"	; Get ( FileMakerPath ) ;
			directory = "file"			; Get ( FilePath ) ;
			directory = "parent"	; Get ( FilePath ) ;
		) ;
	dirPathB =
		Substitute ( dirPathA ;
			["file:/" ; ""] ;
			["file:" ; ""] ;
			["filemac:" ; ""] ;
			["filewin:" ; ""]
		) ;
	directoryPath =
		Case (
			directory = "parent" ; Left ( dirPathB ; Position ( dirPathB ; "/" ; Length ( dirPathB) ; -1 )) ;
			dirPathB
		) ;
	directoryPath =
		Case (
			not IsEmpty ( pathType ) ; directoryPath ;
			Let ( [
				disk = Get ( SystemDrive )
				; posDrive = Position ( directoryPath ; disk ; 1 ; 1 )
				] ;
				If (
					posDrive = 0 ; directoryPath
					; Middle ( directoryPath ; Position ( directoryPath ; "/" ; posDrive + 1 ; 1 ) ; 999 )
				)
		) ;
	file = If ( Left ( file ; 1 ) = "/" ; Middle ( file ; 2 ; 999 ) ; file ) ) ;
	result_rough =
		Case (
//			IsEmpty ( typePath ) ; "Error: Invalid pathType ( " & Quote ( pathType ) & ")" ;
			IsEmpty ( dirPathA ) ; "Error: Invalid directory (" & Quote ( directory ) & ")" ;
			typePath & directoryPath & file
		) ;
	result =
		Case (
			pathType = "web" ;
				GetAsURLEncoded ( result_rough ) ;
			result_rough
		)
	] ;
	result
)

/* —————————————————————————————— //
NAME:
	FileSetPath ( pathType ; directory ; file )
	v2.5

PURPOSE:
	Returns path to pre-designated directory

HISTORY:
	Created 2008.07.29 by Donovan Chandler

DEPENDENCIES:
	Custom Functions:  

INPUT:
	pathType:  Location of the directory [relative, full, remote, network, web]
	directory:	Any directory found by Get ( ) functions
	file:	Name of file (including extension)

	FileSetPath ( "full" ; "desktop" ; "graphic.gif" ) ->> "filemac:/Macintosh HD/Users/apu/Desktop/graphic.gif"

NOTES:
	
*/