Case (

	IsEmpty ( text ) ; Quote ( "" ) ;

	GetAsNumber ( text ) = text ; text ;

	"\""
	& Substitute ( text ;
		  [ "\\"			; "\\\\" ]	// back slash
		; [ "\""			; "\\\"" ]	// double quote
		; [ "/"			; "\/" ]	// forward slash
		; [ Char ( 147 )		; "\\\"" ]	// left curly quote
		; [ Char ( 148 )		; "\\\"" ]	// right curly quote
		; [ Char ( 8220 )	; "\\\"" ]	// left curly quote
		; [ Char ( 8221 )	; "\\\"" ]	// right curly quote
		; [ Char ( 10 )		; "\n" ]	// LF
		; [ "¶" & Char ( 10 )	; "\n" ]	// CR+LF (Windows)

		; [ "\¶"			; "\\¶" ]
		; [ "¶"			; "\n" ]

		//-- Collapse lines for efficiency
	//	; [ "\¶"			; ":‡:" ]
	//	; [ "¶"			; Char ( 32 ) ]	// CR to space
	//	; [ ":‡:"			; "¶" ]
	)
	& "\""

)

/* __________________________________________________

NAME:	JSONEncode ( text )
PURPOSE:	Escapes or removes invalid characters in JSON, wrapping value in double quotes.
TESTS:
	List (
	  JSONEncode ( "Jim's \fun/ place?¶" )	= "\"Jim's \\\fun\/ place?\n\""
	; JSONEncode ( "" ) = "\"\""
	; JSONEncode ( 8 ) = "8"
	)
HISTORY:
	2011-07-22 14:14 PT - Donovan Chandler
	2013-11-03 22:07 PT - Donovan Chandler : Escapes front and back slashes.
	2013-11-17 21:46 PT - Donovan Chandler : Fixed escaping of curly quotes.
	2014-03-09 16:02 PT - Donovan Chandler : No longer quotes numbers.
	2014-03-23 17:38 PT - Donovan Chandler : Quotes empty strings.
	2014-11-20 22:58 ET - Ryan Simms : added LF and CR+LF subs
NOTES:
	Code ( "
	" ) = 32
	Code ( "¶" ) = 13
*/