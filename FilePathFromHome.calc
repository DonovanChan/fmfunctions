Let ( [
	posColon = Position ( path ; ":" ; 1 ; 1 ) ;
	path = If ( posColon = 0 ; path ; Middle ( path ; posColon + 1 ; 999 ) ) ;
	path = If ( Left ( path ; 1 ) ≠ "/" ; path ; Middle ( path ; 2 ; 999 ) ) ;
	hdName = 
		GetValue (
			Substitute ( Get ( DesktopPath ) ; "/" ; ¶ ) ;
			2
		) ;
	hdNameLen = Length ( hdName ) ;
	path = 
		If ( 
			Left ( path ; hdNameLen + 1 ) = hdName & "/" ;
			 	Middle ( path ; hdNameLen + 2 ; 999 ) ;
			path
		)
	];
	path
)

/* —————————————————————————————— //
NAME:
	FilePathFromHome ( path )

PURPOSE:
	Removes prefix and hard drive name from path

EXAMPLES:
	FilePathFromHome ( "filemac:/HD/Users/Donovan/Desktop" )
	= "Users/Donovan/Desktop"

HISTORY:
	Created: 2011-Jun-29 15h39 PST — Donovan A. Chandler
*/