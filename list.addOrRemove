Let(
	[
	value_exists = PatternCount( "¶" & currentList & "¶"; "¶" & value & "¶" )
	];
	Case(
		value_exists;
			MiddleValues(
				Substitute( "¶" & currentList;
				 	[¶; "¶|"];
					["|" & value & "¶"; "¶"];
					["¶|"; ¶]
				);
				2;
				1000000
			);
		
		IsEmpty ( value );
			currentList;
		
		currentList & 
		If( Right( currentList; 1 ) ≠ ¶ and Length ( currentList ); ¶ ) & value
	)
)

/* —————————————————————————————— //
NAME: 
	list.addRemove( currentList; value )

PURPOSE: 
	Adds or removes value from return-delimited list.

HISTORY: 
	Created by Matt Petrowsky of ISO Productions, Inc.
	Adapted 2008jun18 by Donovan Chandler for formatting/documentation preference
	Modified 2010.07.13 by Donovan Chandler to test for empty currentList
	Modified 2010.07.27 by Donovan Chandler to fix bug whereby some values were not being removed

INPUT: 
	currentList:  return-delimited list of values
	value:	text string to be added or removed

OUTPUT: 
   Return-delimited list from currentList parameter with value added or removed
   
NOTES:
   Adds if "value" exists in "currentList" or removes if it does not. Useful for adding/removing an id from a multi-key, such as for portal selections.
*/