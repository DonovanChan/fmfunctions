Let ([
	//-- Settings
	host = "yourHost.com" ;
	port = "" ; //Typically be 8020

	//-- Logic
	cred = If ( NotEmpty ( user ) ; user & ":" & password & "@" ) ;
	subfolder = If ( NotEmpty ( folder ) ; Substitute ( "/" & folder ; "//" ; "/" ) ) ;
	style = Substitute ( style ; ¶ ; "+" ) ;
	paramList =
		List (
			If ( NotEmpty ( height )	; "height=" & height ) ;
			If ( NotEmpty ( width )	; "width=" & width ) ;
			If ( NotEmpty ( resolution )	; "resolution=" & resolution ) ;
			If ( NotEmpty ( style )	; "style=" & style ) ;
			If ( NotEmpty ( backgroundColor ) ; "backgroundColor=" & backgroundColor )
		)
	] ;
	"http://" & cred & host & If ( port ; ":" & port ) &
	"/" & "SuperContainer/" & If ( showRawData ; "RawData" ; "Files" ) & subfolder & "?" &
	Substitute ( paramList ; ¶ ; "&" )
)


/* —————————————————————————————— //
NAME:
	SuperContainerGetFile ( user ; password ; folder ; height ; width ; resolution ; style ; backgroundColor ; showRawData )

PURPOSE:
	Constructs path for retrieving SuperContainer files.
	For more documentation, see the main SuperContainer docs.

EXAMPLES:
	SuperContainerGetFile (
		"user" ; "password" ;
		"Folder/Subfolder" ;
		500 ; 400 ;
		300 ;
		"noapplet+bold¶left" ;
		"rgb(10,10,255)" ;
		False
	)
	="http://user:password@yourServer.com/SuperContainer/Files/Folder/Subfolder?height=500&width=400&resolution=300&style=noapplet+bold&background-color=rgb(10,10,255)"

HISTORY:
	Created: 2011-Mar-18 15h30 PST — Donovan A. Chandler
*/