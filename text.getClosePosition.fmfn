/*---------------------------------------------------------------------------------------
NAME: 
    text.getClosePosition( text; openChar; closeChar; startPosition )

PURPOSE: 
    Finds first openChar after startPosition and returns position of corresponding closeChar.

HISTORY: 
    Created: 2009.07.17 by Donovan Chandler

INPUT: 
    text:    Text string containing open and close character pairs
    openChar:    
    closeChar:    
    startPosition:    

OUTPUT: 
    Integer of position where closeChar occurs.
    Errors return result of 0.
    
---------------------------------------------------------------------------------------*/

Let([
    //-- Determine end point of last search
    numCloseChar = If( not IsEmpty( $$text_numCloseChar ); $$text_numCloseChar; 1 );
    
    sCloseChar = Position( text; closeChar; startPosition; numCloseChar );
    vBlock = Middle( text; startPosition; sCloseChar - startPosition + 1 );
    countOpenChar = PatternCount( vBlock; openChar ); 
    countCloseChar = PatternCount( vBlock; closeChar )
    
    ] ;
    
    Case(
        //--CloseChar is for child element --
        countOpenChar > countCloseChar;
            Let([ $$text_numCloseChar = numCloseChar + 1 ]; "" )
            & text.getClosePosition( text; openChar; closeChar; startPosition );
            
        //-- Result --
        countOpenChar = countCloseChar;
            Let( $$text_numCloseChar = ""; "" )
            & sCloseChar;
            
        //-- Error --
        0
        
    )

)