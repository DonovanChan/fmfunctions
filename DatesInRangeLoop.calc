Let ( [
  start = GetAsDate ( startDate ) ;
  end = GetAsDate ( endDate ) ;
  dayOfWeek = DayOfWeek ( start ) ;
  holidayList = ListDateToNumber ( DatesHolidayList ) ;
  isWeekend =
    Case (
      omitWeekends ;
        dayOfWeek = 7 or dayOfWeek = 1 ;
      0
    ) ;
  isHoliday =
    Case (
      omitHolidays ;
        ValueCount ( FilterValues ( holidayList ; GetAsNumber ( start ) ) ) > 0 ;
      0
    ) ;
  result = 
    Case (
      isWeekend or isHoliday ;
        previousResults ;
      List ( previousResults ; start )
    )
  ] ;
  Case (
    start > end ;
      previousResults ;

    DatesInRangeLoop (
      start + 1 ;
      endDate ;
      omitWeekends ;
      omitHolidays ;
      result
    )

  )
)

/* —————————————————————————————— //
NAME:
  DatesInRangeLoop ( startDate ; endDate ; omitWeekends ; omitHolidays ; previousResults )

PURPOSE:
  Performs recursive operation for DatesInRange().  Returns list of dates as specified. 

EXAMPLES:
  DatesInRangeLoop ( "10/2/2010" ; "10/4/2010" ; 1 ; 0 ; "10/1/2010" ) = "10/1/2010¶10/4/2010"

HISTORY:
  Created: 2010-Oct-15 12h11 PST — Donovan A. Chandler

NOTE:
    omitHolidays parameter currently not supported.
*/