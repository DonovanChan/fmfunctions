Let ( [
	//-- Remove disk from path (e.g., "C:/")
	tag = "!!DISK!!" ;
	drive = Get ( SystemDrive ) ;
	path = Substitute ( path ; drive ; tag ) ;

	//-- Remove prefixes
	posColon = Position ( path ; ":" ; 1 ; 1 ) ;
	path = If ( posColon = 0 ; path ; Middle ( path ; posColon + 1 ; 999 ) ) ;

	//-- Restore disk to path
	path = Substitute ( path ; tag ; drive ) ;

	//-- Remove leading slashes
	path = TextStripLeft ( path ; "/" )
	];
	path
)

/* __________________________________________________

NAME:	PathStripPrefix ( path )
PURPOSE:	Removes prefix from file path.
EXAMPLES:
	// Where desktop path = "/Macintosh HD/Users/donovan/Desktop/"
	PathStripPrefix ( "file:/" & Get ( DesktopPath ) )
	= "/Macintosh HD/Users/donovan/Desktop/"
	
	// Where desktop path = "/C:/Users/Donovan/Desktop/"
	PathStripPrefix ( "file:/" & Get ( DesktopPath ) )
	= "C:/Users/Donovan/Desktop/"
	
	PathStripPrefix ( "fmnet:/your.server.com/FOCUS.fmp12" )
	= "your.server.com/FOCUS.fmp12"
HISTORY:
	Created:	2012-02-03 21:57 PT - Donovan Chandler
	Modified:	2013-01-22 17:24 PT - Donovan Chandler : Windows compatible.
*/