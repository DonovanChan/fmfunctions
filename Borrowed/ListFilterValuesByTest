/*
FilterValuesByTest ( text ; varName ; test ; include_1 ) 

Returns a value list filtered by a test 
version 1.5

By Fabrice Nordmann, BH&A
http://www.bh-a.com

----------
History
1.5 : handles smart quotes
1.4 : renamed parameter IN_or_OUT -> include_1
1.3 : compatibility with FileMaker 8.5
1.2 : optimized version
1.1 : minor bug fix
----------

[text] is the value list to filter.
[varName] is the name you want to use in your calculation (test). Typically set to "value". Beware ! it's case sensitive !
[test] is a bolean expression, between double quotes. e.g : "length ( value ) < 4", or a text field containing that expression.
[include_1] : if set to 1, returns only the values matching the test, else returns only the values not matching the test.

e.g :
       FilterValuesByTest ( "how¶incredible¶is¶that"; "value"; "length ( value ) < 4"; 1 )
          returns : "how¶is" ( words shorter than 4 characters)
       FilterWordsByTest ( "how incredible¶is that"; "value"; "left ( value; 1) = \"i\""; 0 )
          returns : "how incredible¶" (values that do not begin with "i")
*/

If ( ValueCount ( text ) > 0;

Let ([
   _theValue = LeftValues ( Substitute ( text ; [ "\“" ; "\"" ] ; [ "\”" ; "\"" ] ; [ "\"" ; "\\\""] ); 1);
 
    _formattedTest = Lower ( Substitute ( test 
       ; [ varName; "\"" & Substitute ( _thevalue ; ¶; "") & "\""]) );
    _evaluatedTest = Evaluate ( _formattedTest )
];
LeftValues (
 If ( _evaluatedtest = include_1 ; Substitute ( _theValue ; "\\\"" ; "\"" );"" )


& FilterValuesByTest (
   RightValues ( text ; ValueCount ( text ) -1 ); varName; test; include_1 ) ; 999999999999 )
)
)