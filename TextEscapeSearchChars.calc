Substitute ( searchString ;
	[ "\@"	; "@" ] ;
	[ "@"		; "\@" ] ;

	[ "\#"	; "#" ] ;
	[ "#"		; "\#" ] ;

	[ "\*"	; "*" ] ;
	[ "*"		; "\*" ] ;

	[ "\!"	; "!" ] ;
	[ "!"		; "\!" ] ;

	[ "\?"	; "?" ] ;
	[ "?"		; "\?" ] ;

// Disabled because it was destroying the "today" operator (//)
//	[ "\/"	; "/" ] ;
//	[ "/"	; "\/" ] ;

	[ "\<"	; "<" ] ;
	[ "<"	; "\<" ] ;

	[ "\>"	; ">" ] ;
	[ ">"	; "\>" ] ;

	[ "\≤"	; "≤" ] ;
	[ "≤"	; "\<" ] ;

	[ "\≥"	; "≥" ] ;
	[ "≥"	; "\≥" ] ;

	[ "\="	; "=" ] ;
	[ "="	; "\=" ]
)

/* —————————————————————————————— //
NAME:
	TextEscapeSearchChars ( searchString )

PURPOSE:
	Escapes reserved characters in search strings

EXAMPLES:
	TextEscapeSearchChars ( "<20" ) = "\<20"

HISTORY:
	Created: 2011-Mar-01 15h12 PST — Donovan A. Chandler
*/