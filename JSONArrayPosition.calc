Let ( [
	~whitespace = List ( "," ; JSONWhitespace ) ;
	~nodeStart = PositionIgnoring ( text ; ~whitespace ; startPosition ) ;
	~openDelim = Middle ( text ; ~nodeStart ; 1 ) ;
	~closeDelim =
		Case (
			~openDelim = "{" ; "}" ;
			~openDelim = "[" ; "]" ;
			~openDelim = "\"" ; "\""
		) ;
	~nodeEndSearchStart = ~nodeStart + If ( ~closeDelim = "\"" ; 1 ) ;
	~nodeEnd =
		If (
			not IsEmpty ( ~closeDelim ) ;
			TextClosePosition ( text ; ~openDelim ; ~closeDelim ; ~nodeEndSearchStart )
		) ;
	~nextStart = If ( ~nodeEnd ; ~nodeEnd ; ~nodeStart ) + 1
	] ;
	// Used for debugging
	//"~nodeStart=" & ~nodeStart & ", ~openDelim=" & ~openDelim & ", ~nodeEnd=" & ~nodeEnd & Â¶ &

	Case (
		~nodeStart = 0 or ~nodeEnd = 0 ; "?" ;
		index = 1 ; ~nodeStart ;
		JSONArrayPosition ( text ; ~nextStart + 1 ; index - 1 )
	)
)

/* __________________________________________________

NAME:	JSONArrayPosition ( text ; startPosition ; index )
PURPOSE:	Returns start position of Nth value in JSON array.
EXAMPLES:
	Where $_text = "[ {\"a\":\"dog\",\"b\":\"cat\"}, [1,2,3] ]"
	JSONArrayPosition ( $_text ; 1 ; 1 ) = 1 // NOT intended usage
	JSONArrayPosition ( $_text ; 2 ; 1 ) = 7
	JSONArrayPosition ( $_text ; 2 ; 2 ) = 17
HISTORY:
	Created:	2012-10-17 12:51 PT - Donovan Chandler
	Modified:	2013-11-03 23:20 PT - Donovan Chandler : supports number values
NOTES:
	Be sure that text parameter is the *contents* of the array. Omit the enclosing "[" and "]".
*/