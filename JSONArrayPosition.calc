Let ( [
	whitespace =
		","
		& Char ( 32 )	// space
		& Char ( 9 )	// horizontal tab
		& Char ( 10 )	// line feed
		& Char ( 11 )	// vertical tab
		& Char ( 12 )	// form feed
		& Char ( 13 ) ;	// carriate return
	nodeStart = PositionIgnoring ( text ; whitespace ; startPosition ) ;
	openDelim = Middle ( text ; nodeStart ; 1 ) ;
	closeDelim =
		Case (
			openDelim = "{" ; "}" ;
			openDelim = "[" ; "]"
		) ;
	nodeEnd = TextClosePosition ( text ; openDelim ; closeDelim ; nodeStart )
	] ;
	Case (
		nodeStart = 0 or nodeEnd = 0 ; "?" ;
		index = 1 ; nodeStart ;
		JSONArrayPosition ( text ; nodeEnd + 1 ; index - 1 )
	)
)

/* __________________________________________________

NAME:	JSONArrayPosition ( text ; startPosition ; index )
PURPOSE:	Returns start position of Nth value in JSON array.
EXAMPLES:
	Where $_text = "[ {\"a\":\"dog\",\"b\":\"cat\"}, [1,2,3] ]"
	JSONArrayPosition ( $_text ; 1 ; 1 ) = 1 // NOT intended usage
	JSONArrayPosition ( $_text ; 2 ; 1 ) = 7
	JSONArrayPosition ( $_text ; 2 ; 2 ) = 17
HISTORY:
	Created:	2012-10-17 12:51 PT - Donovan Chandler
	Modified:
NOTES:
	Be sure that text parameter is the *contents* of the array. Omit the enclosing "[" and "]".
*/