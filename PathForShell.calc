Let ( [
	path = GetValue ( pathList ; 1 ) ;
	pathCount = ValueCount ( pathList ) ;

	//-- Remove prefixes like "filemac:"
	colonPos = Position ( path ; ":" ; 1 ; 1 ) ;
	slashPos = Position ( path ; "/" ; 1 ; 1 ) ;
	path =
		If (
			colonPos < slashPos ; Middle ( path ; colonPos + 1 ; 999 ) ;
			path
		) ;
	path =
		If (
			Left ( path ; 2 ) = "//" ; Middle ( path ; 2 ; 999 ) ;
			path
		) ;

	//-- Remove disk name
	diskName = Get ( SystemDrive ) ;
	diskNameLen = Length ( diskName ) ;
	pathNew =
		If (
			Left ( path ; diskNameLen ) = diskName ;
				Middle ( path ; diskNameLen ; 999 ) ; // Include a leading "/"
			path
		) ;

	//-- Strip invalid characters
	pathEncoded =
		If (
			not stripInvalidChars ;
				pathNew ;
			Substitute (
				pathNew ;
				[ Â¶	; " " ] ;
				[ "|"	; " " ] ;
				[ "<"	; " " ] ;
				[ ">"	; " " ] ;
				[ ".."	; "." ] ;
				[ "\""	; "" ] ;
				[ ","	; "" ]
			)
		) ;

	//-- Escape characters for shell
	pathEncoded =
		Substitute ( pathEncoded ;
			[ "'"	; "'\"'\"'" ]
		) ;
	pathEncoded = "'" & pathEncoded & "'"
	];
	List (
		If ( not IsEmpty ( pathEncoded ) ; pathEncoded ) ;
		If ( pathCount > 1 ;
			PathForShell ( RightValues ( pathList ; pathCount - 1 ) ; stripInvalidChars )
		)
	)
)

/* __________________________________________________

NAME:	PathForShell ( pathList ; stripInvalidChars )
PURPOSE:	Returns path usable in shell
TESTS:
	List (
	  PathForShell ( "/tmp/1A-2B@me.com/foo 5:23.xlsx" ; 0 ) = "'/tmp/1A-2B@me.com/foo 5:23.xlsx'"
	; PathForShell ( "bar/foo.txt" ; 0 ) = "'bar/foo.txt'"
	; PathForShell ( "file:tmp/foo.txt" ; 0 ) = "'tmp/foo.txt'"
	; PathForShell ( "filemac:/tmp/foo.txt" ; 0 ) = "'/tmp/foo.txt'"
	; PathForShell ( "/tmp/Bob's data" ; 0 ) = "'/tmp/Bob'\"'\"'s data'"
	//; PathForShell ( "filewin:/C:/tmp/foo.txt" ; 0 ) = "'C:/tmp/foo.txt'"
	//; PathForShell ( "filewin://computerName/share/tmp/foo.txt" ; 0 ) = "'computerName/share/tmp/foo.txt"'
	//; PathForShell ( "fmnet:/172.0.0.1/foo.txt" ; 0 ) = "'172.0.0.1/foo.txt'"
	)
NOTES:
	Not tested on Windows or network paths.
HISTORY:
	2011-11-04 14:28 PST - Donovan Chandler
	2014-03-03 17:45 PST - Donovan Chandler : Removed dependency on TextEncodeForFilePath().
	2014-05-14 11:39 PDT - Donovan Chandler : Use single quotes instead of escaping spaces, etc.
	2014-05-29 10:27 PDT - Donovan Chandler : Less aggressive removing prefix.
*/