Let([
	//-- Determine end point of last search
	numCloseChars = If ( not IsEmpty ( $$text_numCloseChars ) ; $$text_numCloseChars ; 1 ) ;

	sCloseChars = Position( text ; closeChars ; startPosition ; numCloseChars ) ;
	vBlock = Middle ( text ; startPosition ; sCloseChars - startPosition + 1 ) ;
	countopenChars = PatternCount ( vBlock ; openChars ) ;
	countCloseChars = PatternCount ( vBlock ; closeChars )

	] ;

	Case(
		//-- CloseChars is for child element
		countopenChars > countCloseChars ;
			Let ( [ $$text_numCloseChars = numCloseChars + 1 ] ; "" )
			& TextClosePosition ( text ; openChars ; closeChars ; startPosition ) ;

		//-- Result
		countOpenChars = countCloseChars ;
			Let ( $$text_numCloseChars = "" ; "" )
			& sCloseChars ;

		//-- Error
		0

	)

)

/* __________________________________________________

NAME:	TextClosePosition( text ; openChars ; closeChars ; startPosition )
PURPOSE:	Finds first openChars after startPosition and returns position of corresponding closeChars.
EXAMPLES:
	Where $_text = "[ [1,2], [3,4] ]"

	TextClosePosition ( $_text ; "[" ; "]" ; 1 ) = 16
	TextClosePosition ( $_text ; "[" ; "]" ; 2 ) = 7
HISTORY:
	Created:	2009-07-17 12:00 PT - Donovan Chandler
OUTPUT:
	Integer of position where closeChars occurs.
	Errors return result of 0.
*/