Let ( [
	tagOpen = "‡‡" ;
	tagClose = "‡‡" ;
	tagOpenLen = Length ( tagOpen ) ;
	tagCloseLen = Length ( tagClose ) ;
	posMarker = Position ( text ; tagOpen & "CODE" ; 1 ; 1 ) ;
	endMarkerType = Position ( text ; "[" ; posMarker ; 1 ) - 1 ;
	endMarker = Position ( text ; tagClose ; posMarker + 1 ; 1 ) + tagCloseLen - 1 ;
	marker = Middle ( text ; posMarker ; endMarker + tagCloseLen - 1 - posMarker ) ;
	markerType = Middle ( text ; posMarker + tagOpenLen  ; endMarkerType - posMarker - tagCloseLen + 1 ) ;
	index = GetAsNumber ( marker ) ;
	code =
		Case (
			markerType = "CODE" ; FOCUS::CODE_INCLUDE_rep[index] ;
			markerType = "CODE_LINK" ;
				CodeMerge (
					Substitute ( FOCUS::CODE_LINK_rep[index] ;
						[ tagOpen & "CODE" & tagClose ; FOCUS::CODE_rep[index] ] ;
						[ tagOpen & "NAME" & tagClose ; FOCUS::CODE_FILE_NAMESPACE & FOCUS::CODE_FILE_NAME_rep[index] ]
					)
				) ;
			markerType = "CODE_NAME" ;
				CodeMerge (
					Substitute ( FOCUS::CODE_FILE_NAME_rep[index] ;
						[ tagOpen & "CODE" & tagClose ; FOCUS::CODE_rep[index] ] ;
						[ tagOpen & "NAME" & tagClose ; FOCUS::CODE_FILE_NAMESPACE & FOCUS::CODE_FILE_NAME_rep[index] ]
					)
				) ;
			//-- Modify tag so it doesn't get processed again
			"INVALID CODE LABEL: " & Substitute ( marker ; [ tagOpen ; tagOpen & " " ] ; [ tagClose ; " " & tagClose ] )
		) ;
	textNew =
		If (
			posMarker = 0 ; text ;
			Substitute ( text ; marker ; code )
		)
	] ;
	Case (
		Position ( textNew ; tagOpen & "CODE" ; 1 ; 1 ) ;
			CodeMerge ( textNew ) ;
		textNew
	)
)

/* __________________________________________________

NAME:	CodeMerge ( text )
PURPOSE:	Merges value from LIBRARY_CODE table into text.
		Useful for on-the-fly merging of code libraries in a web viewer.
EXAMPLES:
	// Where FOCUS::LIBRARY_CODE_INCLUDE[1] = "Beezwax!"
	// Where FOCUS::LIBRARY_CODE_LINK[1] = "http://beezwax.net"
	CodeMerge ( "Hello ‡‡CODE[1]‡‡" ) = "Hello Beezwax!"
	CodeMerge ( "Hello ‡‡CODE_LINK[1]‡‡" ) = "http://beezwax.net"
HISTORY:
	Created:	2010-08-27 21:06 PT - Donovan Chandler
	Modified:	2013-11-18 14:18 PT - Donovan Chandler : Merges NAME and CODE tags.
	Modified:	2013-12-16 18:26 PT - Donovan Chandler : Easier to change tag format.
*/