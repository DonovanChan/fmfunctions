Let([
	path = GetValue ( pathList ; 1 ) ;
	pathCount = ValueCount ( pathList ) ;
	
	//-- Remove prefixes
	posColon = Position ( path ; ":" ; 1 ; 1 ) ;
	path = If ( posColon = 0 ; path ; Middle ( path ; posColon + 1 ; 999 ) ) ;
	path = If ( Left ( path ; 1 ) ≠ "/" ; path ; Middle ( path ; 2 ; 999 ) ) ;
	
	//-- Remove disk name
	hdName = 
		GetValue (
			Substitute ( Get ( DesktopPath ) ; "/" ; ¶ ) ;
			2
		) ;
	hdNameLen = Length ( hdName ) ;
	pathNew = 
		If ( 
			Left ( path ; hdNameLen + 1 ) = hdName & "/" ;
			 	Middle ( path ; hdNameLen + 2 ; 999 ) ;
			path
		) ;
	pathEncoded = If ( not encode ; pathNew ; TextEncodeForFilePath ( pathNew ) )
	];
	List (
		If ( not IsEmpty ( pathEncoded ) ; pathEncoded ) ;
		If ( pathCount > 1 ;
			FilePathForScriptMaster ( RightValues ( pathList ; pathCount - 1 ) ; encode )
		)
	)
)

/* —————————————————————————————— //
NAME:
	FilePathForScriptMaster ( pathList ; encode )

PURPOSE:
	Returns path usable for ScriptMaster.
	Does NOT support Windows.
	Does NOT support external drives.

EXAMPLES:
	Where $_path = "filemac:/Macintosh HD/Users/Donovan/Desktop/test, 1.txt"

	FilePathForScriptMaster ( $_path ; True )
	= "/Users/Donovan/Desktop/test 1.txt"

	FilePathForScriptMaster ( $_path ; False )
	= "/Users/Donovan/Desktop/test, 1.txt"

HISTORY:
	Created: 2011-Feb-24 21h13 PST — Donovan A. Chandler
	Modified: 2011-Jul-13 12h30 PST — Donovan A. Chandler ; improved removal of disk name
*/