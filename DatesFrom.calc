Let ([
  //-- Build list of valid dates
  list_new =
    Case (
      numberOfDaysToReturn ≠ 0 ;
        DatesFromLoop (
          GetAsDate ( startDate ) ;
          numberOfDaysToReturn ;
          omitWeekends ;
          ListDateToNumber ( omitDateList ) ;
          ""
        )
    )
  ];
  //-- Ensure list is sorted ascending
  Case (
    IsEmpty ( list_new ) ;
      "" ;
    numberOfDaysToReturn < 0 ;
      ListSortDates ( list_new ; 0 ) ;
    list_new
  )
)

/* —————————————————————————————— //
NAME:
  DatesFrom ( startDate ; numberOfDaysToReturn ; omitWeekends ; omitDateList )

PURPOSE:
  Returns list of dates in range. Excludes weekends and/or holidays if specified.
  Pass negative number into numberOfDaysToReturn to end range on startDate.

EXAMPLES:
  DatesFrom ( "10/1/2010" ; 2 ; 1 ; "10/1/2010" ) = "10/4/2010¶10/5/2010"
  DatesFrom ( "10/1/2010" ; -2 ; 1 ; "" ) = "9/29/2010¶9/30/2010"

HISTORY:
  Created: 2010-Oct-15 12h06 PST — Donovan A. Chandler
  
NOTE:
  
*/