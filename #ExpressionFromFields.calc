Let ( [
  ex = 
    "Let([ nameList = " & Quote( listOfFieldNames ) & "; " &
      "nameFull = GetValue( nameList; [n] ); " &
      "name = FieldNameShort( nameFull ); " &
      "valueRough = GetField( nameFull ); " &
      "value = If( valueRough = \"?\" or IsEmpty( valueRough ); \"\"; valueRough ) " &
      "]; " &
      "\"# ( \" & Quote( name ) & \" ; \" & TextAsCalculation ( value ) & \" )\" " &
    ")" ;
  list = ListCustom ( 1 ; ValueCount ( listOfFieldNames ) ; ex )
  ];
  Substitute ( list ; ")¶" ; ") &¶" )
)

/* —————————————————————————————— //
NAME:
	#ExpressionFromFields ( listOfFieldNames )

PURPOSE:
	Returns hash expression, each pair named after field with value containing field contents.
	Results can then be executed using the Evaluate() function.
	Useful for displaying calculation for inspection or demo purposes.

EXAMPLES:
	HashFromFields ( "CONTACT::NAME¶CONTACT::PHONE" )
	= #( "NAME" ; "Fred" ) & #( "PHONE" ; "971.555.1234" )

DEPENDENCIES:
	Custom Functions: #(), FieldNameShort()

HISTORY:
	Created: 2010-Nov-02 08h50 PST — Donovan A. Chandler
*/