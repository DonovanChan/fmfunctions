Let ( [
	_S = If ( GetAsBoolean ( isGlobal ) ; "$$" ; "$" ) ;
	exError = "IsEmpty ( " & _S & name & ") or " & _S & name & " = \"?\"" ;
	exSet =
		Case (
			IsEmpty ( type ) or type = "text" or type = "t" ;
				"GetAsText ( " ;
			type = "number" or type = "n" ;
				"GetAsNumber ( " ;
			type = "date" or type = "d" ;
				"GetAsDate ( " ;
			type = "time" or type = "i" ;
				"GetAsTime ( " ;
			type = "timestamp" or type = "m" ;
				"GetAsTimestamp ( " ;
			type = "GetAsBoolean" or type ="b" ;
				"GetAsBoolean ( "
		)
	] ;
	Evaluate (
		"Let([" & _S & name & "=" & exSet & Quote ( value ) & If ( not IsEmpty ( exSet ) ; " )" ) & " ];" & exError & ")"
	)
)

/* __________________________________________________

NAME:	VarSet ( name ; value ; type ; isGlobal )
PURPOSE:	Instantiates local or global variable, casting for type and performing validation.
		Returns "1" if value is empty or equals "?", else "0".
EXAMPLES:
	// Set $$_tab_list to "tab_1" as text
	VarSet ( "_tab_list" ; "tab_1" ; "" ; 1 ) // = 0
	
	// Attempt to set $date to "1/1/12" as date
	VarSet ( "date" ; "1/1/" ; "d" ; 1 ) // = 1
HISTORY:
	Created:	2012-01-23 15:07 PST - Donovan Chandler
	Modified:	2012-04-20 11:43 PST - Donovan Chandler ; added GetAsBoolean()
*/